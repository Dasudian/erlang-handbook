\chapter{Сопоставление с образцом}
\label{patterns}

\section{Переменные}
\label{patterns:variables}

\textbf{Переменные} представлены, как аргументы функции или как результат 
сопоставления с образцом. Переменные начинаются с заглавной буквы или символа
подчёркивания (\texttt{\_}) и могут содержить буквенно-цифровые символы, 
подчёркивания и символы \emph{at} (\texttt{@}). Переменные могут быть связаны со 
значением (присвоены) только один раз.

\begin{erlang}
Abc
A_long_variable_name
AnObjectOrientatedVariableName
_Height
\end{erlang}

\textbf{Анонимная переменная} объявляется с помощью одного символа подчёркивания
(\texttt{\_}) и может использоваться там, где требуется переменная, но её значение
нас не интересует и может быть проигнорировано.

\begin{erlang}
[H|_] = [1,2,3]         % H=1 и всё остальное игнорируется
\end{erlang}

Переменные, начинающиеся с символа подчёркивания, как, например, 
\texttt{\_Height}, являются обычными не анонимными переменными.  Однако они
игнорируются компилятором в том смысле, что они не произведут предупреждений
компилятора о неиспользуемых переменных.  Таким образом, возможна следующая
запись:

\begin{erlang}
member(_Elem, []) ->
    false.
\end{erlang}

вместо:

\begin{erlang}
member(_, []) ->
    false.
\end{erlang}

что улучшает читаемость кода.

\textit{Область видимости} для переменной --- это её уравнение функции.  
Переменные, связанные со значением в ветке \texttt{if}, \texttt{case} или 
\texttt{receive}, должны быть связаны с чем-нибудь во всех ветвях этого 
оператора, чтобы иметь значение за пределами выражения, иначе компилятор будет
считать это значение \textit{небезопасным} (unsafe) (вероятно, не присвоенным)
за пределами этого выражения, и выдаст соответствующее предупреждение.


\section{Сопоставление с образцом}

\textbf{Образец} имеет такую же структуру, как и терм, но может содержать новые
свободные переменные. Например:

\begin{erlang}
Name1
[H|T]
{error,Reason}
\end{erlang}

Образцы могут встречаться в \textit{заголовках функций}, выражениях 
\textit{case}, \textit{receive}, и \textit{try} и в выражениях оператора
сопоставления (\texttt{=}). Образцы вычисляются посредством 
\textbf{сопоставления образца} с выражением, и таким образом новые переменные
определяются и связываются со значением.

\begin{erlangru}
Образец = Выражение
\end{erlangru}

Обе стороны выражения должны иметь одинаковую структуру. Если сопоставление 
проходит успешно, то все свободные переменные (если такие были) в образце слева
становятся связанными. Если сопоставление не проходит, то возникает ошибка
времени исполнения \texttt{badmatch}.


\begin{minted}{console}
> {A, B} = {answer, 42}.
{answer,42}
> A.
answer
> B.
42
\end{minted}


\subsection{Оператор сопоставления (\texttt{=}) в образцах}

Если \texttt{Образец1} и \texttt{Образец2} являются действительными образцами, тогда следующая запись тоже действительный образец:

\begin{erlangru}
Образец1 = Образец2
\end{erlangru}

Оператор \texttt{=} представляет собой \textbf{подмену} (alias), при
сопоставлении которой с выражением, оба и \texttt{Образец1} и \texttt{Образец2}
также сопоставляются с ней. Цель этого --- избежать необходимости повторно
строить термы, которые были разобраны на составляющие в сопоставлении.

\begin{erlang}
foo({connect,From,To,Number,Options}, To) ->
    Signal = {connect,From,To,Number,Options},
    fox(Signal),
    ...;
\end{erlang}

можно более эффективно записать, как:

\begin{erlang}
foo({connect,From,To,Number,Options} = Signal, To) ->
    fox(Signal),
    ...;
\end{erlang}


\subsection{Строковой префикс в образцах}

При сопоставлении строк с образцом, следующая запись является действительным 
образцом:

\begin{erlang}
f("prefix" ++ Str) -> ...
\end{erlang}

что эквивалентно и легче читается, чем следующая запись:

\begin{erlang}
f([$p,$r,$e,$f,$i,$x | Str]) -> ...
\end{erlang}

Вы можете использовать строки только как префикс; варианты с постфиксом для 
образцов, такие как \texttt{Str ++ "postfix"} не разрешаются. 


\subsection{Выражения в образцах}

Арифметическое выражение может быть использовано внутри образца, если оно 
использует только числовые, битовые операторы, и его значение является 
константой, которая может быть вычислена во время компиляции.

\begin{erlang}
case {Value, Result} of
    {?Threshold+1, ok} -> ...   % ?Threshold - это макрос
\end{erlang}


\subsection{Сопоставление двоичных данных}

\begin{erlang}
Bin = <<1, 2, 3>>      % <<1,2,3>> Все элементы - 8-битные байты
<<A, B, C>> = Bin      % A=1, B=2 и C=3
<<D:16, E>> = Bin      % D=258 и E=3
<<F, G/binary>> = Bin  % F=1 и G=<<2,3>>
\end{erlang}

В последней строке переменная \texttt{G} неуказанного размера сопоставляется с 
остатком двоичных данных \texttt{Bin}.

Всегда ставьте пробел между оператором (\texttt{=}) и (\verb|<<|), чтобы 
избежать возможной путаницы с оператором (\texttt{=<}).

